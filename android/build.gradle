buildscript {
    ext {
        buildToolsVersion = "33.0.2"
        minSdkVersion = 28
        compileSdkVersion = 33
        targetSdkVersion = 33
        ndkVersion = "25.2.9519653"
        cmakeVersion = "3.22.1"
        javaVersion = JavaVersion.VERSION_17
        kotlinVersion = "1.8.21"
    }
    repositories {
        maven {
            url("$rootDir/maven")
        }
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle")
        classpath("com.facebook.react:react-native-gradle-plugin")
    }
}

allprojects {
    repositories {
        maven {
            url("$rootDir/maven")
        }
    }
}

subprojects { subproject ->
    afterEvaluate {
        java {
            sourceCompatibility = rootProject.ext.javaVersion
            targetCompatibility = rootProject.ext.javaVersion
        }
        if (subproject.hasProperty("android")) {
            android {
                buildToolsVersion rootProject.ext.buildToolsVersion
                compileSdkVersion rootProject.ext.compileSdkVersion
                ndkVersion rootProject.ext.ndkVersion
                externalNativeBuild {
                    cmake {
                        version rootProject.ext.cmakeVersion
                    }
                }
                defaultConfig {
                    minSdkVersion rootProject.ext.minSdkVersion
                    targetSdkVersion rootProject.ext.targetSdkVersion
                }
                compileOptions {
                    sourceCompatibility = rootProject.ext.javaVersion
                    targetCompatibility = rootProject.ext.javaVersion
                }
                if (subproject.plugins.hasPlugin("org.jetbrains.kotlin.android")) {
                    kotlinOptions {
                        jvmTarget = rootProject.ext.javaVersion.toString()
                    }
                }
            }
        }
    }
}
